<!DOCTYPE html>
<html>
  <head>
    <title>Exploding Teapots</title>
    <link rel="stylesheet" type="text/css" href="styles.css">
  </head>
  <body>
    <!-- Canvas scene is rendered on -->
    <canvas id="webgl-canvas"></canvas>
    <!-- Sliders -->
    <div id=num-teapots class=slider-label><b>Teapots:</b> 1</div>
    <input id=num-teapots-slider
           class=slider
           type=range
           min=1 max=10
           value=1
           step=1 >
    </input>
    <div id=num-tessellations class=slider-label><b>Tessellations:</b> 0</div>
    <input id=num-tessellations-slider
           class=slider
           type=range
           min=0 max=3
           value=0
           step=1 >
    </input>

    <!-- Vertex shader -->
    <script type="x-shader/x-vertex" id="shader-vs">
      precision mediump float;

      attribute vec3 a_Position;
      attribute vec3 a_Offset;
      attribute vec3 a_Normal;
      attribute float a_Random;
      attribute vec3 a_Axis;

      // World position of vertices gets passed to fragment shader
      varying vec3 w_Position;
      // Transformed normal vectors also get passed to the fragment shader
      varying vec3 t_Normal;

      uniform float expTime; // Explosion animation timer
      uniform mat4 tMatrix; // Translation matrix (for multiple teapots)
      uniform mat4 vMatrix; // View matrix
      uniform mat4 pMatrix; // Perspective matrix

      // Get vertex position
      vec3 getVertexPosition() {
        vec3 offset;
        vec3 normalPos;
        vec3 normalAxis;
        float theta;
        mat3 rotation;
        vec3 pos;

        offset = normalize(a_Offset);

        normalPos = normalize(a_Position);
        normalAxis = normalize(a_Axis);

        theta = pow(expTime, 0.5) * a_Random;
        rotation = mat3( cos(theta) + pow(normalAxis.x, 2.)*(1.-cos(theta)),
                         normalAxis.y*normalAxis.x*(1.-cos(theta)) + normalAxis.z*sin(theta),
                         normalAxis.z*normalAxis.x*(1.-cos(theta)) - normalAxis.y*sin(theta),
                         normalAxis.x*normalAxis.y*(1.-cos(theta)) - normalAxis.z*sin(theta),
                         cos(theta) + pow(normalAxis.y, 2.)*(1.-cos(theta)),
                         normalAxis.z*normalAxis.y*(1.-cos(theta)) + normalAxis.x*sin(theta),
                         normalAxis.x*normalAxis.z*(1.-cos(theta)) + normalAxis.y*sin(theta),
                         normalAxis.y*normalAxis.z*(1.-cos(theta)) + normalAxis.x*sin(theta),
                         cos(theta) + pow(normalAxis.z, 2.)*(1.-cos(theta)) );

        pos = a_Position - a_Offset;
        pos = rotation * pos + a_Offset;

        pos += pow(expTime, 1.25) * clamp(a_Random, 0.25, 1.) * offset / 1000.;
        pos = vec3((tMatrix * vec4(pos, 1.)).xyz);
        return pos;
      }

      void main() {
        vec3 position3d = getVertexPosition();
        w_Position = position3d;
        t_Normal = normalize(a_Normal);
        gl_Position = pMatrix * vMatrix * vec4(position3d, 1.);
      }
    </script>

    <!-- Fragment shader -->
    <script type="x-shader/x-fragment" id="shader-fs">
      precision mediump float;

      // Position of the camera
      uniform vec3 cameraPos;

      // World position of each vertex
      varying vec3 w_Position;
      // Transformed normal vectors
      varying vec3 t_Normal;

      // Lights
      vec3 lightPos[3];
      vec3 lightCol[3];
      float lightIntensities[3];


      // Fragment shader uses Gourard shading
      // and Blinn-Phong global illumination model

      // Determining the diffuse color of the teapot
      vec3 diffuseColor() {
        vec3 diffColor;
        diffColor = vec3(0.);

        for(int i = 0; i < 3; i++) {
          vec3 color;
          vec3 lightDir;
          float lambertian;

          color = vec3(0.5, 1., 0.9);
          lightDir = lightPos[i] - w_Position;
          lambertian = dot(normalize(lightDir), t_Normal);
          lambertian *= lightIntensities[i];
          lambertian /= pow(length(lightDir), 2.);
          lambertian = clamp(lambertian, 0.2, 1.);

          diffColor += lambertian * color * lightCol[i];
        }

        return diffColor;
      }

      // Determining the specular color of the teapot
      vec3 specularColor() {
        vec3 specColor;
        specColor = vec3(0.);

        for(int i = 0; i < 3; i++) {
          vec3 lightDir;
          vec3 viewDir;
          vec3 H;
          float specular;

          lightDir = lightPos[i] - w_Position;
          viewDir = w_Position - cameraPos;
          H = normalize(lightDir + viewDir);
          specular = pow(max(dot(t_Normal, H), 0.), 200.);
          specular *= lightIntensities[i];
          specular /= pow(length(lightDir), 2.);

          specColor += specular * vec3(.7);
        }

        return specColor;
      }

      void main() {
        lightPos[0] = vec3(18., 15., 30.);
        lightPos[1] = vec3(-15., 10., 30.);
        lightPos[2] = vec3(-20., 5., -2.);

        lightCol[0] = vec3(1., 0.7, 0.5);
        lightCol[1] = vec3(0.3, 0.7, 1.);
        lightCol[2] = vec3(0., 0.1, 0.3);

        lightIntensities[0] = 1200.;
        lightIntensities[1] = 400.;
        lightIntensities[2] = 600.;

        gl_FragColor = vec4(diffuseColor() + specularColor(), 1.);
      }
    </script>

    <!-- JavaScript -->
    <script type="text/javascript" src="js/lib/cuon-matrix.js"></script>
    <script type="text/javascript" src="js/lib/web-gl-loader.js"></script>
    <script type="text/javascript" src="js/lib/jquery-2.1.4.min.js"></script>
    <script type="text/javascript" src="js/Teapots.js"></script>
    <script type="text/javascript" src="js/Shaders.js"></script>
    <script type="text/javascript" src="js/Buffers.js"></script>
    <script type="text/javascript" src="js/Scene.js"></script>
    <script type="text/javascript" src="js/main.js"></script>
  </body>
</html>
